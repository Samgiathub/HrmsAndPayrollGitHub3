


---02/2/2021 (EDIT BY MEHUL ) (SP WITH NOLOCK)---
CREATE PROCEDURE [dbo].[SP_RPT_Attendance_Card] 
	 @Cmp_ID		numeric
	,@From_Date		datetime
	,@To_Date		datetime 	
	,@Branch_ID		varchar(Max) 
	,@Cat_ID		varchar(Max)
	,@Grd_ID		varchar(Max) 	
	,@Type_ID		varchar(Max) 
	,@Dept_ID		varchar(Max) 
	,@Desig_ID		varchar(Max) 
	,@Emp_ID		numeric  = 0
	,@Constraint	varchar(max) = ''
	,@Salary_Cycle_id numeric = NULL
	,@New_Join_emp	numeric = 0 
	,@Left_Emp		Numeric = 0	
	,@Segment_Id  varchar(Max) = ''		
	,@Vertical_Id varchar(Max) = ''		
	,@SubVertical_Id varchar(Max) = ''		
	,@SubBranch_Id varchar(Max) = ''	
	
AS
BEGIN
SET NOCOUNT ON 
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
SET ARITHABORT ON

	CREATE TABLE #EMP_CONS 
	(      
	   EMP_ID NUMERIC ,     
	   BRANCH_ID NUMERIC,
	   INCREMENT_ID NUMERIC    
	)  
 
	EXEC SP_RPT_FILL_EMP_CONS_MULTIDROPDOWN @CMP_ID,@FROM_DATE,@TO_DATE,@BRANCH_ID,@CAT_ID,@GRD_ID,@TYPE_ID,@DEPT_ID,@DESIG_ID,@EMP_ID,@CONSTRAINT,0,@SALARY_CYCLE_ID,@SEGMENT_ID,@VERTICAL_ID,@SUBVERTICAL_ID,@SUBBRANCH_ID,@NEW_JOIN_EMP,@LEFT_EMP,0,'0',0,0    
 
	IF OBJECT_ID('TEMPDB..#ATT_MUSTER_EXCEL') IS NOT NULL	
		DROP TABLE #ATT_MUSTER_EXCEL
 
 
	 CREATE TABLE #ATT_MUSTER_EXCEL 
	 (	
		EMP_ID		NUMERIC , 
		CMP_ID		NUMERIC,
		FOR_DATE	DATETIME,
		STATUS		VARCHAR(10) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		LEAVE_COUNT	NUMERIC(5,2),
		WO_HO		VARCHAR(3) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		STATUS_2	VARCHAR(20) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		ROW_ID		NUMERIC ,
		WO_HO_DAY	NUMERIC(3,2) DEFAULT 0,
		P_DAYS		NUMERIC(5,2) DEFAULT 0,
		A_DAYS		NUMERIC(5,2) DEFAULT 0 ,
		JOIN_DATE	DATETIME DEFAULT NULL,
		LEFT_DATE	DATETIME DEFAULT NULL,
		GATE_PASS_DAYS NUMERIC(18,2) DEFAULT 0,  -- ADDED BY GADRIWALA MUSLIM 07042015
		LATE_DEDUCT_DAYS NUMERIC(18,2) DEFAULT 0, -- ADDED BY GADRIWALA MUSLIM 07042015
		EARLY_DEDUCT_DAYS NUMERIC(18,2) DEFAULT 0, -- ADDED BY GADRIWALA MUSLIM 07042015
		EMP_CODE    VARCHAR(50) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		EMP_FULL_NAME  VARCHAR(300) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		BRANCH_ADDRESS VARCHAR(300) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		COMP_NAME VARCHAR(200) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		BRANCH_NAME VARCHAR(200) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		DEPT_NAME  VARCHAR(200) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		GRD_NAME VARCHAR(200) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		DESIG_NAME VARCHAR(200) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS,
		P_FROM_DATE  DATETIME,
		P_TO_DATE DATETIME,
		BRANCH_ID NUMERIC(18,0),
		DESIG_DIS_NO NUMERIC(18,2) DEFAULT 0,          ---ADDED JIMIT 31082015 
		SUBBRANCH_NAME VARCHAR(200) DEFAULT '' COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS
	 )	
 
	CREATE NONCLUSTERED INDEX IX_DATA ON DBO.#ATT_MUSTER_EXCEL(EMP_ID,EMP_CODE,ROW_ID) 
 
	IF(@EMP_ID <> 0 AND (@CONSTRAINT = '0' OR @CONSTRAINT='' ))
		BEGIN
			SET @CONSTRAINT  = @EMP_ID
		END	
 
	EXEC SP_RPT_EMP_ATTENDANCE_MUSTER_GET @CMP_ID,@FROM_DATE,@TO_DATE,@BRANCH_ID,
										  @CAT_ID,@GRD_ID,@TYPE_ID,@DEPT_ID,@DESIG_ID,
										  @EMP_ID,@CONSTRAINT,'','EXCEL'
										  
	DECLARE @DynamicPivotQuery AS NVARCHAR(MAX)
	DECLARE @ColumnName AS NVARCHAR(MAX)
	
	select @ColumnName = STUFF((SELECT distinct ',' + QUOTENAME(ROW_ID) 
                    from #ATT_MUSTER_EXCEL
            FOR XML PATH(''), TYPE
            ).value('.', 'NVARCHAR(MAX)') 
        ,1,1,'')
	
	SET @DynamicPivotQuery = 'SELECT Emp_ID, ' + @ColumnName + '
							FROM   (SELECT [Emp_ID], [STATUS],
				   [ROW_ID]
			FROM   [#ATT_MUSTER_EXCEL]) AS t
		   PIVOT (MAX([STATUS])
				 FOR [ROW_ID] IN (' + @ColumnName + ')) AS p'
				 
	set @DynamicPivotQuery = 'select * into ##pivotAttendanceData from ('+ @DynamicPivotQuery +') y '
	
	EXEC(@DynamicPivotQuery)
	
	--select * from ##pivotAttendanceData
	--Ronakb300824 add branch,vertical,subvertical,subbranch
	
	SELECT I_Q.* ,E.EMP_LAST_NAME,E.EMP_SECOND_NAME, E.STREET_1,E.CITY,E.STATE,E.EMP_FULL_NAME ,E.WORKER_ADULT_NO,E.FATHER_NAME, E.EMP_CODE,E.ALPHA_EMP_CODE,E.EMP_FIRST_NAME,LEFT_DATE,BM.COMP_NAME,BM.BRANCH_ADDRESS,LEFT_REASON
				,DEPT_NAME,DESIG_NAME,TYPE_NAME,GRD_NAME,BRANCH_NAME,SB.SubBranch_Name,SV.SubVertical_Name,DATE_OF_JOIN,DATE_OF_BIRTH,EMP_MARK_OF_IDENTIFICATION,GENDER,@FROM_DATE AS FROM_DATE ,@TO_DATE AS TO_DATE
				,CMP_NAME,CMP_ADDRESS,PRESENT_STREET,PRESENT_STATE,PRESENT_CITY,PRESENT_POST_BOX,L.LEFT_REASON,DATEDIFF(YY,ISNULL(DATE_OF_BIRTH,GETDATE()),GETDATE()) AS AGE,
				NATURE_OF_BUSINESS,CMP_CITY,CMP_STATE_NAME,CMP_PINCODE,E.MOBILE_NO--,I_Q.BANK_ID,I_Q.INC_BANK_AC_NO
				,VS.VERTICAL_NAME,WO.WEEKOFF_DAY,SM.SHIFT_NAME,PAD.*
	FROM DBO.T0080_EMP_MASTER E WITH (NOLOCK) LEFT OUTER JOIN DBO.T0100_LEFT_EMP L WITH (NOLOCK) ON E.EMP_ID =  L.EMP_ID INNER JOIN
		( SELECT I.EMP_ID , GRD_ID,BRANCH_ID,SubBranch_ID,SubVertical_ID,CAT_ID,DESIG_ID,DEPT_ID,TYPE_ID,BANK_ID,INC_BANK_AC_NO,VERTICAL_ID FROM DBO.T0095_INCREMENT I WITH (NOLOCK) INNER JOIN 
				( SELECT MAX(INCREMENT_ID) AS INCREMENT_ID , EMP_ID FROM DBO.T0095_INCREMENT WITH (NOLOCK)	-- ANKIT 10092014 FOR SAME DATE INCREMENT
				WHERE INCREMENT_EFFECTIVE_DATE <= @TO_DATE
				AND CMP_ID = @CMP_ID
				GROUP BY EMP_ID  ) QRY ON
				I.EMP_ID = QRY.EMP_ID AND I.INCREMENT_ID = QRY.INCREMENT_ID	 ) I_Q 
			ON E.EMP_ID = I_Q.EMP_ID  
				INNER JOIN
				DBO.T0040_GRADE_MASTER GM WITH (NOLOCK) ON I_Q.GRD_ID = GM.GRD_ID LEFT OUTER JOIN
				DBO.T0040_TYPE_MASTER ETM WITH (NOLOCK) ON I_Q.TYPE_ID = ETM.TYPE_ID LEFT OUTER JOIN
				DBO.T0040_DESIGNATION_MASTER DGM WITH (NOLOCK) ON I_Q.DESIG_ID = DGM.DESIG_ID LEFT OUTER JOIN
				DBO.T0040_DEPARTMENT_MASTER DM WITH (NOLOCK) ON I_Q.DEPT_ID = DM.DEPT_ID INNER JOIN 
				DBO.T0030_BRANCH_MASTER BM WITH (NOLOCK) ON I_Q.BRANCH_ID = BM.BRANCH_ID  INNER JOIN 
				DBO.T0010_COMPANY_MASTER CM WITH (NOLOCK) ON E.CMP_ID = CM.CMP_ID LEFT OUTER JOIN
				DBO.T0050_SubVertical SV WITH (NOLOCK) ON I_Q.SubVertical_ID = SV.SubVertical_ID LEFT OUTER JOIN
				DBO.T0050_SubBranch SB WITH (NOLOCK) ON I_Q.SubBranch_ID = SB.SubBranch_ID INNER JOIN
				#EMP_CONS EC ON E.EMP_ID = EC.EMP_ID LEFT JOIN
				( 
					SELECT W.EMP_ID,W.WEEKOFF_DAY 
						FROM DBO.T0100_WEEKOFF_ADJ W WITH (NOLOCK)
					INNER JOIN ( 
									SELECT MAX(FOR_DATE) AS FOR_DATE,EMP_ID 
										FROM DBO.T0100_WEEKOFF_ADJ WITH (NOLOCK)	 
									WHERE FOR_DATE <= @TO_DATE AND CMP_ID = @CMP_ID
									GROUP BY EMP_ID  
							    ) QRY ON W.EMP_ID = QRY.EMP_ID AND W.FOR_DATE = QRY.FOR_DATE	 
				) WO ON EC.EMP_ID = WO.EMP_ID
				 INNER JOIN  
				( 
					SELECT SH.EMP_ID,SH.SHIFT_ID  
						FROM DBO.T0100_EMP_SHIFT_DETAIL SH WITH (NOLOCK)
					INNER JOIN( 
								SELECT MAX(FOR_DATE) AS FOR_DATE,EMP_ID 
									FROM DBO.T0100_EMP_SHIFT_DETAIL	 WITH (NOLOCK)
								WHERE FOR_DATE <= @TO_DATE AND CMP_ID = @CMP_ID
								GROUP BY EMP_ID  
							  ) QRY ON SH.EMP_ID = QRY.EMP_ID AND SH.FOR_DATE = QRY.FOR_DATE	 
				) SHO ON EC.EMP_ID = SHO.EMP_ID 
				INNER JOIN DBO.T0040_SHIFT_MASTER SM WITH (NOLOCK) ON SM.SHIFT_ID = SHO.SHIFT_ID
				LEFT OUTER JOIN DBO.T0040_VERTICAL_SEGMENT VS WITH (NOLOCK) ON VS.VERTICAL_ID = I_Q.VERTICAL_ID
				INNER JOIN ##pivotAttendanceData PAD ON PAD.Emp_ID = E.Emp_ID
						WHERE E.CMP_ID = @CMP_ID	
		--ORDER BY RIGHT(REPLICATE(N' ', 500) + E.ALPHA_EMP_CODE, 500) 
		ORDER BY CASE WHEN ISNUMERIC(E.ALPHA_EMP_CODE) = 1 THEN RIGHT(REPLICATE('0',21) + E.ALPHA_EMP_CODE, 20)
		WHEN ISNUMERIC(E.ALPHA_EMP_CODE) = 0 THEN LEFT(E.ALPHA_EMP_CODE + REPLICATE('',21), 20)
			ELSE E.ALPHA_EMP_CODE
		END
		
		
	Drop TABLE ##pivotAttendanceData
END


