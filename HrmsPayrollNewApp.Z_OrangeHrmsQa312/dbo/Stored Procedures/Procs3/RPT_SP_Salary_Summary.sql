--====================================================
-- Created By HARDIK ON 16/12/2020 FOR SALARY SUMMARY REPORT FOR HMP CLIENT
--====================================================
CREATE PROCEDURE [dbo].[RPT_SP_Salary_Summary]
	 @Cmp_ID 		numeric
	,@From_Date		datetime
	,@To_Date 		datetime
	,@Branch_ID		varchar(max)=''
	,@Cat_ID 		varchar(max)='' 
	,@Grd_ID 		varchar(max)=''
	,@Type_ID 		varchar(max)=''
	,@Dept_ID 		varchar(max)=''
	,@Desig_ID 		varchar(max)=''
	,@Emp_ID 		numeric
	,@constraint 	varchar(MAX)
	,@Segment_Id  varchar(max)='' 
	,@Vertical_Id varchar(max)=''
	,@SubVertical_Id varchar(max)=''
	,@SubBranch_Id varchar(max)=''
AS
	SET NOCOUNT ON 
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
	SET ARITHABORT ON
	
	DECLARE @PF_DEF_ID INT
	DECLARE @ESIC_DEF_ID INT
	DECLARE @CMP_ESIC_DEF_ID INT

	SET @PF_DEF_ID = 2
	SET @ESIC_DEF_ID = 3
	SET @CMP_ESIC_DEF_ID = 6
		
	IF @Branch_ID = '0' or @Branch_ID=''
		set @Branch_ID = null
		
	IF @Cat_ID = '0' or @Cat_ID='' 
		set @Cat_ID = null

	IF @Grd_ID = '0' or @Grd_ID=''  
		set @Grd_ID = null

	IF @Type_ID = '0' or @Type_ID=''  
		set @Type_ID = null

	IF @Dept_ID = '0' or @Dept_ID='' 
		set @Dept_ID = null

	IF @Desig_ID = '0' or @Desig_ID=''  
		set @Desig_ID = null

	IF @Emp_ID = 0  
		set @Emp_ID = null

	If @Segment_Id = '0' or @Segment_Id=''
		set @Segment_Id = null
		
	If @Vertical_Id = '0' or @Vertical_Id=''		 
		set @Vertical_Id = null
		
	If @SubVertical_Id = '0' or @SubVertical_Id='' 	 
		set @SubVertical_Id = null	
		
	If @SubBranch_Id = '0' or @SubBranch_Id=''
		set @SubBranch_Id = null	
	    
	 CREATE TABLE #Emp_Cons
	 (
		Emp_ID	numeric,
		Branch_ID numeric,
		Increment_ID numeric
	 )  

	EXEC SP_RPT_FILL_EMP_CONS_MULTIDROPDOWN @Cmp_ID,@From_Date,@To_Date,@Branch_ID,@Cat_ID,@Grd_ID,@Type_ID,@Dept_ID,@Desig_ID,@Emp_ID,@constraint,0,0,@Segment_Id,@Vertical_Id,@SubVertical_Id,@SubBranch_Id,0,0,0,'',0,0    	

	ALTER TABLE #EMP_CONS ADD Gender varchar(10)

	UPDATE EC 
	SET Gender = CASE E.Gender  WHEN 'M' THEN 'MALE' WHEN 'F' THEN 'FEMALE' END
	FROM #Emp_Cons EC INNER JOIN T0080_EMP_MASTER E  WITH (NOLOCK) ON EC.Emp_ID = E.Emp_ID

	
	CREATE TABLE #EMP_PF_DETAIL
	(
		EPF_COUNT_MALE NUMERIC,
		EPF_COUNT_FEMALE NUMERIC,
		EPF_WAGES_MALE NUMERIC(18,0),
		EPF_WAGES_FEMALE NUMERIC(18,0),
		EPF_EXEMPTED_MALE NUMERIC,
		EPF_EXEMPTED_FEMALE NUMERIC,
		EPF_TOTAL_EMP_MALE NUMERIC,
		EPF_TOTAL_EMP_FEMALE NUMERIC,
		EPF_AMOUNT_MALE NUMERIC(18,2),
		EPF_AMOUNT_FEMALE NUMERIC(18,2),
		EPF_WAGES_EXEMPTED_MALE NUMERIC(18,2),
		EPF_WAGES_EXEMPTED_FEMALE NUMERIC(18,2)
	)

	CREATE TABLE #EMP_ESIC_DETAIL
	(
		ESIC_COUNT_MALE NUMERIC,
		ESIC_COUNT_FEMALE NUMERIC,
		ESIC_WAGES_MALE NUMERIC(18,0),
		ESIC_WAGES_FEMALE NUMERIC(18,0),
		ESIC_EXEMPTED_MALE NUMERIC,
		ESIC_EXEMPTED_FEMALE NUMERIC,
		ESIC_TOTAL_EMP_MALE NUMERIC,
		ESIC_TOTAL_EMP_FEMALE NUMERIC,
		ESIC_AMOUNT_MALE NUMERIC(18,2),
		ESIC_AMOUNT_FEMALE NUMERIC(18,2),
		CESIC_AMOUNT_MALE NUMERIC(18,2),
		CESIC_AMOUNT_FEMALE NUMERIC(18,2),
		ESIC_WAGES_EXEMPTED_MALE NUMERIC(18,2),
		ESIC_WAGES_EXEMPTED_FEMALE NUMERIC(18,2)
	)

	CREATE TABLE #EMP_LWF_DETAIL
	(
		LWF_COUNT NUMERIC,
		LWF_EMPLOYEE_SHARE NUMERIC(18,2),
		LWF_EMPLOYER_SHARE NUMERIC(18,2),
		LWF_TOTAL_SHARE NUMERIC(18,2)
	)

	CREATE TABLE #EMP_PT_DETAIL
	(
		PT_TOTAL_COUNT NUMERIC,
		PT_TOTAL_AMOUNT NUMERIC(18,2),
		PT_SLAB_AMOUNT NUMERIC(18,2),
		PT_COUNT NUMERIC,
		PT_AMOUNT NUMERIC(18,2)
	)

	CREATE TABLE #Tbl_Get_AD
	(
		EMP_ID NUMERIC(18,0),
		AD_ID NUMERIC(18,0),
		FOR_DATE DATETIME,
		E_AD_PERCENTAGE NUMERIC(18,5),
		E_AD_AMOUNT NUMERIC(18,2)
	)

	DECLARE @CONST VARCHAR(MAX)
	DECLARE @CONST_BRANCH_ID VARCHAR(MAX)

	SELECT @CONST = COALESCE(@CONST + '#','') + CAST(S.EMP_ID AS varchar(10)),
		@CONST_BRANCH_ID = COALESCE(@CONST_BRANCH_ID + '#','') + CAST(EC.Branch_ID AS varchar(10))
	FROM T0200_MONTHLY_SALARY S WITH (NOLOCK)
		INNER JOIN #Emp_Cons EC ON S.Emp_ID = EC.Emp_ID
	WHERE S.CMP_ID = @Cmp_ID AND MONTH(S.Month_End_Date) = MONTH(@TO_DATE) AND YEAR(S.Month_End_Date) = YEAR(@TO_DATE)
	
	INSERT INTO #Tbl_Get_AD
	Exec P_Emp_Revised_Allowance_Get @CMP_id,@TO_DATE,@CONST

	Select T.* Into #Tbl_Get_AD_PF from #Tbl_Get_AD T Inner Join T0050_AD_MASTER A WITH (NOLOCK) On T.AD_ID = A.AD_ID
	Where A.AD_DEF_ID = @PF_DEF_ID

	Select T.* Into #Tbl_Get_AD_ESIC from #Tbl_Get_AD T Inner Join T0050_AD_MASTER A WITH (NOLOCK) On T.AD_ID = A.AD_ID
	Where A.AD_DEF_ID = @ESIC_DEF_ID


	--- BELOW PORTION FOR EMPLOYEE PF
	INSERT INTO #EMP_PF_DETAIL
	SELECT COUNT(CASE WHEN EC.GENDER = 'MALE' AND MAD.M_AD_Amount > 0 THEN 1 ELSE NULL END) AS EPF_COUNT_MALE,
			COUNT(CASE WHEN EC.GENDER = 'FEMALE' AND MAD.M_AD_Amount > 0 THEN 1 ELSE NULL END) AS EPF_COUNT_FEMALE,
			SUM(CASE WHEN EC.GENDER = 'MALE' AND MAD.M_AD_Amount > 0 THEN MAD.M_AD_Calculated_Amount ELSE 0 END) AS EPF_WAGES_MALE,
			SUM(CASE WHEN EC.GENDER = 'FEMALE' AND MAD.M_AD_Amount > 0 THEN MAD.M_AD_Calculated_Amount ELSE 0 END) AS EPF_WAGES_FEMALE,
			--COUNT(CASE WHEN EC.GENDER = 'MALE' AND ISNULL(MAD.M_AD_Amount,0) = 0 THEN 1 ELSE NULL END) AS EPF_EXEMPTED_MALE,
			--COUNT(CASE WHEN EC.GENDER = 'FEMALE' AND MAD.M_AD_Amount = 0 THEN 1 ELSE NULL END) AS EPF_EXEMPTED_FEMALE
			0,0,0,0,
			SUM(CASE WHEN EC.GENDER = 'MALE' AND MAD.M_AD_Amount > 0 THEN MAD.M_AD_Amount ELSE 0 END) AS EPF_WAGES_MALE,
			SUM(CASE WHEN EC.GENDER = 'FEMALE' AND MAD.M_AD_Amount > 0 THEN MAD.M_AD_Amount ELSE 0 END) AS EPF_WAGES_FEMALE,
			0,0
	FROM T0210_MONTHLY_AD_DETAIL MAD WITH (NOLOCK)
		INNER JOIN T0050_AD_MASTER A WITH (NOLOCK) ON MAD.AD_ID = A.AD_ID
		INNER JOIN #Emp_Cons EC ON MAD.Emp_ID = EC.Emp_ID
	WHERE MAD.CMP_ID = @Cmp_ID AND A.AD_DEF_ID = @PF_DEF_ID AND
		MONTH(MAD.To_date) = MONTH(@TO_DATE) AND YEAR(MAD.To_date) = YEAR(@TO_DATE) AND MAD.S_Sal_Tran_ID IS NULL



	--SELECT A.AD_DEF_ID,EC.*,T.AD_ID
	--FROM #Emp_Cons EC
	--	Left Outer JOIN #Tbl_Get_AD_PF T ON T.Emp_ID = EC.Emp_ID
	--	Left Outer join T0050_AD_MASTER A WITH (NOLOCK) On T.Ad_ID = A.AD_ID And A.AD_DEF_ID = @PF_DEF_ID		
	--WHERE  A.AD_DEF_ID IS NULL and ec.emp_id=24743


	UPDATE #EMP_PF_DETAIL 
	SET 
		EPF_EXEMPTED_MALE= QRY.EPF_EXEMPTED_MALE,
		EPF_EXEMPTED_FEMALE = QRY.EPF_EXEMPTED_FEMALE,
		EPF_TOTAL_EMP_MALE = ISNULL(EPF_COUNT_MALE,0) + ISNULL(QRY.EPF_EXEMPTED_MALE,0),
		EPF_TOTAL_EMP_FEMALE = ISNULL(EPF_COUNT_FEMALE,0) + ISNULL(QRY.EPF_EXEMPTED_FEMALE,0),
		EPF_WAGES_EXEMPTED_MALE = ISNULL(QRY.EPF_WAGES_EXEMPTED_MALE,0),
		EPF_WAGES_EXEMPTED_FEMALE = ISNULL(QRY.EPF_WAGES_EXEMPTED_FEMALE,0)
	FROM
	(SELECT COUNT(CASE WHEN EC.GENDER = 'MALE' THEN 1 ELSE NULL END) AS EPF_EXEMPTED_MALE, 
			COUNT(CASE WHEN EC.GENDER = 'FEMALE' THEN 1 ELSE NULL END) AS EPF_EXEMPTED_FEMALE,
			SUM(CASE WHEN EC.GENDER = 'MALE' THEN Basic_Salary ELSE NULL END) AS EPF_WAGES_EXEMPTED_MALE, 
			SUM(CASE WHEN EC.GENDER = 'FEMALE' THEN Basic_Salary ELSE NULL END) AS EPF_WAGES_EXEMPTED_FEMALE					
	FROM #Emp_Cons EC
		Left Outer JOIN #Tbl_Get_AD_PF T ON T.Emp_ID = EC.Emp_ID
		Left Outer join T0050_AD_MASTER A WITH (NOLOCK) On T.Ad_ID = A.AD_ID And A.AD_DEF_ID = @PF_DEF_ID	
		INNER JOIN T0200_MONTHLY_SALARY MS WITH (NOLOCK) ON EC.Emp_ID = MS.Emp_ID
	WHERE A.AD_DEF_ID IS NULL
		AND MONTH(Month_End_Date) = MONTH(@TO_DATE) AND YEAR(Month_End_Date) = YEAR(@TO_DATE)
	) QRY
	
	                                
	--BELOW PORTION FOR ESIC
	INSERT INTO #EMP_ESIC_DETAIL
	SELECT COUNT(CASE WHEN EC.GENDER = 'MALE' AND MAD.M_AD_Amount > 0 AND A.AD_DEF_ID = @ESIC_DEF_ID THEN 1 ELSE NULL END) AS ESIC_COUNT_MALE,
			COUNT(CASE WHEN EC.GENDER = 'FEMALE' AND MAD.M_AD_Amount > 0 AND A.AD_DEF_ID = @ESIC_DEF_ID THEN 1 ELSE NULL END) AS ESIC_COUNT_FEMALE,
			SUM(CASE WHEN EC.GENDER = 'MALE' AND MAD.M_AD_Amount > 0 AND A.AD_DEF_ID = @ESIC_DEF_ID THEN MAD.M_AD_Calculated_Amount ELSE 0 END) AS ESIC_WAGES_MALE,
			SUM(CASE WHEN EC.GENDER = 'FEMALE' AND MAD.M_AD_Amount > 0 AND A.AD_DEF_ID = @ESIC_DEF_ID THEN MAD.M_AD_Calculated_Amount ELSE 0 END) AS ESIC_WAGES_FEMALE,
			0,0,0,0,
			SUM(CASE WHEN EC.GENDER = 'MALE' AND MAD.M_AD_Amount > 0 AND A.AD_DEF_ID = @ESIC_DEF_ID THEN MAD.M_AD_Amount ELSE 0 END) AS ESIC_WAGES_MALE,
			SUM(CASE WHEN EC.GENDER = 'FEMALE' AND MAD.M_AD_Amount > 0 AND A.AD_DEF_ID = @ESIC_DEF_ID THEN MAD.M_AD_Amount ELSE 0 END) AS ESIC_WAGES_FEMALE,
			SUM(CASE WHEN EC.GENDER = 'MALE' AND MAD.M_AD_Amount > 0 AND A.AD_DEF_ID = @CMP_ESIC_DEF_ID THEN MAD.M_AD_Amount ELSE 0 END) AS CESIC_WAGES_MALE,
			SUM(CASE WHEN EC.GENDER = 'FEMALE' AND MAD.M_AD_Amount > 0 AND A.AD_DEF_ID = @CMP_ESIC_DEF_ID THEN MAD.M_AD_Amount ELSE 0 END) AS CESIC_WAGES_FEMALE,
			0,0
	FROM T0210_MONTHLY_AD_DETAIL MAD WITH (NOLOCK)
		INNER JOIN T0050_AD_MASTER A WITH (NOLOCK) ON MAD.AD_ID = A.AD_ID
		INNER JOIN #Emp_Cons EC ON MAD.Emp_ID = EC.Emp_ID
	WHERE MAD.CMP_ID = @Cmp_ID AND A.AD_DEF_ID IN (@ESIC_DEF_ID,@CMP_ESIC_DEF_ID) AND
		MONTH(MAD.To_date) = MONTH(@TO_DATE) AND YEAR(MAD.To_date) = YEAR(@TO_DATE) AND MAD.S_Sal_Tran_ID IS NULL

	UPDATE #EMP_ESIC_DETAIL 
	SET 
		ESIC_EXEMPTED_MALE= QRY.ESIC_EXEMPTED_MALE,
		ESIC_EXEMPTED_FEMALE = QRY.ESIC_EXEMPTED_FEMALE,
		ESIC_TOTAL_EMP_MALE = ISNULL(ESIC_COUNT_MALE,0) + ISNULL(QRY.ESIC_EXEMPTED_MALE,0),
		ESIC_TOTAL_EMP_FEMALE = ISNULL(ESIC_COUNT_FEMALE,0) + ISNULL(QRY.ESIC_EXEMPTED_FEMALE,0),
		ESIC_WAGES_EXEMPTED_MALE = ISNULL(QRY.ESIC_WAGES_EXEMPTED_MALE,0),
		ESIC_WAGES_EXEMPTED_FEMALE = ISNULL(QRY.ESIC_WAGES_EXEMPTED_FEMALE,0)
	FROM
	(SELECT COUNT(CASE WHEN EC.GENDER = 'MALE' THEN 1 ELSE NULL END) AS ESIC_EXEMPTED_MALE, 
			COUNT(CASE WHEN EC.GENDER = 'FEMALE' THEN 1 ELSE NULL END) AS ESIC_EXEMPTED_FEMALE,
			SUM(CASE WHEN EC.GENDER = 'MALE' THEN Gross_Salary ELSE NULL END) AS ESIC_WAGES_EXEMPTED_MALE, 
			SUM(CASE WHEN EC.GENDER = 'FEMALE' THEN Gross_Salary ELSE NULL END) AS ESIC_WAGES_EXEMPTED_FEMALE					
	FROM #Emp_Cons EC
		Left Outer JOIN #Tbl_Get_AD_ESIC T ON T.Emp_ID = EC.Emp_ID
		Left Outer join T0050_AD_MASTER A WITH (NOLOCK) On T.Ad_ID = A.AD_ID AND A.AD_DEF_ID = @ESIC_DEF_ID
		INNER JOIN T0200_MONTHLY_SALARY MS WITH (NOLOCK) ON EC.Emp_ID = MS.Emp_ID
	WHERE A.AD_DEF_ID IS NULL
		AND MONTH(Month_End_Date) = MONTH(@TO_DATE) AND YEAR(Month_End_Date) = YEAR(@TO_DATE)
	) QRY

	
	--BELOW PORTION FOR LWF
	INSERT INTO #EMP_LWF_DETAIL
	SELECT COUNT(S.Emp_ID), SUM(S.LWF_AMOUNT), 
		SUM(CASE WHEN UPPER(SM.STATE_NAME) = 'MAHARASHTRA' THEN S.LWF_AMOUNT * 3 ELSE S.LWF_Amount * 2 END),
		SUM(S.LWF_AMOUNT) + SUM(CASE WHEN UPPER(SM.STATE_NAME) = 'MAHARASHTRA' THEN S.LWF_AMOUNT * 3 ELSE S.LWF_Amount * 2 END)
	FROM T0200_MONTHLY_SALARY S WITH (NOLOCK)
		INNER JOIN #Emp_Cons EC ON S.Emp_ID = EC.Emp_ID
		INNER JOIN T0095_INCREMENT I WITH (NOLOCK) ON EC.Increment_ID = I.Increment_ID
		INNER JOIN T0030_BRANCH_MASTER BM WITH (NOLOCK) ON I.Branch_ID = BM.Branch_ID
		LEFT OUTER JOIN T0020_STATE_MASTER SM WITH (NOLOCK) ON BM.State_ID = SM.State_ID
	WHERE S.CMP_ID = @Cmp_ID AND MONTH(S.Month_End_Date) = MONTH(@TO_DATE) AND YEAR(S.Month_End_Date) = YEAR(@TO_DATE)
		AND S.LWF_Amount > 0
	
	-- BELOW PORTION FOR PT COUNT
	INSERT INTO #EMP_PT_DETAIL
	SELECT 0,0,AMOUNT,ISNULL(PT_COUNT,0), AMOUNT * ISNULL(PT_COUNT,0)
	FROM 
	(
		SELECT DISTINCT Amount,PT_COUNT 
		FROM T0040_PROFESSIONAL_SETTING P WITH (NOLOCK) INNER JOIN
			(SELECT MAX(for_Date) AS For_Date 
				FROM T0040_professional_setting WITH (NOLOCK)
				WHERE Cmp_ID =@Cmp_ID and For_Date <= @To_Date
			) QRY ON P.For_Date = QRY.For_Date LEFT OUTER JOIN 
			(SELECT PT_Amount, COUNT(S.EMP_ID) PT_COUNT
			FROM T0200_MONTHLY_SALARY S WITH (NOLOCK) INNER JOIN #Emp_Cons EC ON S.EMP_ID = EC.Emp_ID
			WHERE MONTH(Month_End_Date) = MONTH(@To_Date) AND YEAR(Month_End_Date) = YEAR(@To_Date) AND CMP_ID=@Cmp_ID AND PT_Amount>0
			GROUP BY PT_Amount) QRY1 ON QRY1.PT_Amount = P.Amount
	)PT

	UPDATE #EMP_PT_DETAIL
	SET PT_TOTAL_COUNT = PT_TOTAL_C,
		PT_TOTAL_AMOUNT = PT_TOTAL_A
	FROM 
		(SELECT SUM(PT_COUNT) AS PT_TOTAL_C,  SUM(PT_AMOUNT) AS PT_TOTAL_A
		FROM #EMP_PT_DETAIL PT) QRY

	--- END FOR PT CALCULATION


	SELECT * FROM #EMP_PF_DETAIL
	SELECT * FROM #EMP_ESIC_DETAIL
	SELECT * FROM #EMP_LWF_DETAIL
	SELECT * FROM #EMP_PT_DETAIL

	-- BELOW PORTION FOR PAYMENT MODE COUNT
	SELECT I.PAYMENT_MODE,COUNT(MS.EMP_ID) AS EMP_COUNT, SUM(MS.NET_AMOUNT) AS NET_AMOUNT
	FROM T0200_MONTHLY_SALARY MS WITH (NOLOCK) INNER JOIN
		#Emp_Cons EC ON MS.Emp_ID = EC.Emp_ID INNER JOIN
		T0095_INCREMENT I WITH (NOLOCK) ON EC.Increment_ID = I.Increment_ID AND EC.Emp_ID = I.Emp_ID
	WHERE MONTH(Month_End_Date) = MONTH(@To_Date) AND YEAR(Month_End_Date) = YEAR(@To_Date) AND MS.CMP_ID=@Cmp_ID
	GROUP BY I.Payment_Mode

	--- BELOW PORTION FOR PF CHALLAN
	DECLARE @FORMAT TINYINT = 5 ---For PF Challan
	EXEC [dbo].[SP_RPT_STATUTORY_FORM_3A_GET_EXPORT_TEXT] @Cmp_ID=@Cmp_Id,@From_Date =@From_Date,@To_Date = @To_Date, @Branch_ID = @Branch_ID,@Cat_ID=@Cat_ID,@Grd_ID=@Grd_ID,@Type_ID=@Type_ID,@Dept_ID= @Dept_ID,@Desig_ID=@Desig_ID,@Emp_ID=@Emp_ID,@constraint=@constraint,@Segment_Id=@Segment_Id,@Vertical_Id=@Vertical_Id,@SubVertical_Id=@SubVertical_Id,@SubBranch_Id=@SubBranch_Id,@Format=@Format


	----BELOW PORTION FOR COMPANY DETAIL COMMON DETAILS
	SELECT @From_Date AS From_Date, @To_Date as To_Date, Cmp_Id, Cmp_Name, Cmp_Address, cmp_logo FROM T0010_COMPANY_MASTER WHERE CMP_ID = @Cmp_ID

	DROP TABLE #TBL_GET_AD
	DROP TABLE #EMP_PF_DETAIL
	DROP TABLE #EMP_ESIC_DETAIL
	DROP TABLE #EMP_PT_DETAIL
RETURN




